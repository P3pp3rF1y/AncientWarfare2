import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.1"
    id "org.sonarqube" version "2.6.2"
}

// We apply the 'forge' plugin, so ForgeGradle can do it's stuff.
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "ChickenBones Maven"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name "LatvianModder Ivy"
        url = "https://maven.latmod.com/"
    }
    maven { 
        url "http://dvs1.progwml6.com/files/maven" 
    }
    maven {
        url "http://tehnut.info/maven"
    }
    ivy {
        name "infinitylib"
        ivyPattern 'http://www.google.com/FAKE_URL/ugly_workaround_because_curseforge_403.txt'
        artifactPattern "http://media.forgecdn.net/files/${infinitylib_cf}/[module]-[revision].[ext]"
    }
    ivy {
        name "twilightforest"
        ivyPattern 'http://www.google.com/FAKE_URL/ugly_workaround_because_curseforge_403.txt'
        artifactPattern "http://media.forgecdn.net/files/${twilightforest_cf}/[module]-${project.minecraft_version}-[revision]-universal.[ext]"
    }
}

minecraft{
    version = minecraft_version + "-" + forge_version
    mappings = mcp_mappings
    runDir = "run"
    replaceIn "automation/AncientWarfareAutomation.java"
    replaceIn "core/AncientWarfareCore.java"
    replaceIn "npc/AncientWarfareNPC.java"
    replaceIn "structure/AncientWarfareStructures.java"
    replaceIn "vehicle/AncientWarfareVehicles.java"
    replace "@VERSION@", "${project.minecraft.version}-${project.major}.${project.minor}.${project.patch}." + getBuildNumber() + getStable()
}
version = minecraft.version + "-" + project.major +"."+ project.minor +"."+ project.patch +"."+ getBuildNumber() + getStable()
group = "shadowmage"
archivesBaseName = "ancientwarfare"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    deobfCompile "cofh:RedstoneFlux:${rf_version}:universal"
    deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}"
    deobfCompile "codechicken:CodeChickenLib:${ccl_version}:deobf"
    deobfCompile "com.feed_the_beast.mods:FTBLib:${ftblib_version}"
    deobfCompile name: 'infinitylib', version: infinitylib_version, ext: 'jar'
    deobfCompile "com.infinityraider.agricraft:AgriCraft:${agricraft_version}"
    deobfCompile name: 'twilightforest', version: twilightforest_version, ext: 'jar'
}

processResources{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes 'FMLAT': 'ancientwarfare_at.cfg'
    }
}

def getBuildNumber() {
    if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
        return System.getenv("TRAVIS_BUILD_NUMBER").toString()
    }
    return ""
}

def getStable() {
    if ((System.getenv("DRONE_BRANCH") == null || System.getenv("DRONE_BRANCH").endsWith("-dev"))
            && (System.getenv("TRAVIS_BRANCH") == null || System.getenv("TRAVIS_BRANCH").endsWith("-dev"))) {
        return "unstable"
    }
    return ""
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    filesSpec {
        from 'build/libs'
        into '.'
        exclude '*sources.jar'
    }
    pkg {
        version {
            name = "${project.version}"
            released = new Date()
        }
        repo = 'maven'
        name = 'AncientWarfare2'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/P3pp3rF1y/AncientWarfare2.git'
    }

}

sonarqube {
    properties {
        property "sonar.projectName", "Ancient Warfare 2"
        property "sonar.projectKey", "ancientwarfare"
    }
}
